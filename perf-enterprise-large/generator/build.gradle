plugins {
    id 'groovy'
    id 'idea'
    id 'application'
}

mainClassName = 'org.gradle.generator.enterprise.PerformanceTestGenerator'

repositories {
    jcenter()
}

configurations {
    mirror
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'

    mirror 'junit:junit:4.12'
    mirror 'org.hamcrest:hamcrest-parent:1.3'
    mirror 'org.projectlombok:lombok:1.16.14'
    mirror 'org.slf4j:slf4j-api:1.7.21'
    mirror 'org.slf4j:slf4j-simple:1.7.21'
    mirror 'org.slf4j:slf4j-parent:1.7.21'
}

task mirrorDependencies(type: OfflineMavenRepository) {
    configurationName = 'mirror'
    repoDir = file('../mavenRepo')
}

run {
    if (project.hasProperty('projectSizeFactor')) {
        logger.lifecycle "Passing project size factor ${project.property('projectSizeFactor')}"
        args project.property('projectSizeFactor')
    }
    if (project.hasProperty('appArgs')) {
        logger.lifecycle "appArgs is deprecated, use projectSizeFactor to pass the argument"
        args project.property('appArgs').split(',')
    }
}

run.finalizedBy mirrorDependencies
