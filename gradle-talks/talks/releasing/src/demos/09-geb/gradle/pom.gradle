/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
	pomModifications = []
	modifyPom = { pomModifications << it }
	applyPomMods = { poms ->
		poms.each { pom ->
			pomModifications.each { mod ->
				mod = mod.clone()
				mod.delegate = pom
				mod.resolveStrategy = Closure.DELEGATE_FIRST
				mod(pom)
			}
		}
	}
}

modifyPom {
	project {
		url "http://www.gebish.org"
		inceptionYear "2010"
		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}
		scm {
			url "https://github.com/geb/geb/"
		}
		developers {
			developer {
				id "ldaley"
				name "Luke Daley"
				roles {
					role "Lead"
					role "Founder"
				}
			}
			developer {
				id "erdi"
				name "Marcin Erdmann"
				roles {
					role "Committer"
				}
			}
			developer {
				id "robfletcher"
				name "Rob Fletcher"
				roles {
					role "Founder"
				}
			}
		}
	}
	
	dependencies.removeAll(dependencies.findAll { it.scope == "test" })
}
