apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "maven"
apply from: "maven-customise.gradle"
apply plugin: "signing" // http://www.dewinter.com/gnupg_howto/english/GPGMiniHowto-3.html#ss3.1

// ~/.gradle/gradle.properties
// signing.keyId=«keyid»
// signing.secretKeyRingFile=/Users/ld/.gnupg/secring.gpg
// signing.password=«password»

version = "1.0"
group = "conf.gr8"
archivesBaseName = "foo"

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.8.6'
    compile 'commons-logging:commons-logging:1.1.1'
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8' 
}

task sourceJar(type: Jar) {
    classifier "source"
    from sourceSets.main.allSource
}

task testsJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

task javadocJar(type: Jar) {
    from groovydoc
    classifier "javadoc"
}

task manualZip(type: Zip) {
    classifier "manual"
    from "src/manual"
}

artifacts {
    archives sourceJar, testsJar, javadocJar
}

signing {
    sign configurations.archives
    required { gradle.taskGraph.hasTask(uploadArchives) && !version.endsWith("SNAPSHOT") }
    tasks.withType(Upload) {
        repositories.withType(MavenResolver) {
            beforeDeployment { signPom(it) }
        }
    }
}

modifyPom {
    version = 10
    dependencies.removeAll(dependencies.findAll { it.scope == "test" })

    project {
        developers {
            developer {
                id "ldaley"
                name "Luke Daley"
                roles {
                    role "Doer of things"
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${buildDir}/m2repo")
        }
    }
}
