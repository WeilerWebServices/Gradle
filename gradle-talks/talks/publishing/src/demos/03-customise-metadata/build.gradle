apply plugin:'java'
apply plugin:'idea'
apply plugin:'maven'

apply plugin:'ivy-publish'
apply plugin:'maven-publish'

group "org.gradle"
version "1.0"

repositories{
    mavenCentral()
}

dependencies{
    compile 'org.slf4j:slf4j-api:1.7.5'

    testCompile 'junit:junit:4.11'
}

// Publishing 2.0
publishing {
    repositories {
        ivy {
            url file("repo/ivy-publish")
        }
        maven {
            url file("repo/maven-publish")
        }
    }
    publications {
        ivy(IvyPublication) {
            from components.java

            descriptor.withXml {
                asNode().info[0].@status = 'release'
                asNode().dependencies[0].dependency[0].version[0].value = '1.7.+'
            }
        }
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().version + {
                    resolveStrategy = DELEGATE_ONLY
                    description "A demonstration project for publishing"
                    organization {
                        name "Gradleware"
                        url "http://gradleware.com"
                    }
                }
            }
        }
    }
}

// Publishing 1.0
uploadArchives {
    repositories {
        ivy {
            url file("repo/ivy")
        }

        mavenDeployer {
            repository(url: file("repo/maven").toURI())
            pom.project {
                description "A demonstration project for publishing"
                organization {
                    name "Gradleware"
                    url "http://gradleware.com"
                }
            }
        }
    }
}

task writePomOnly << {
    uploadArchives.repositories.mavenDeployer.pom.writeTo("build/testpom.xml")
}