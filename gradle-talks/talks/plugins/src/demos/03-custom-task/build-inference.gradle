buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.pegdown:pegdown:1.1.0"
    }
}

task writeMarkdown {
    ext.md = file("$buildDir/generated/some.md")

    outputs.files md
    doLast {
        md.parentFile.mkdirs() 
        md.text = "This is a **paragraph**."
    }
}

task markdown(type: Markdown) {
    markdown = files(writeMarkdown)
    html file("$buildDir/some.html")
    encoding "utf8"
}

import org.pegdown.PegDownProcessor

class Markdown extends DefaultTask {
    @InputFiles FileCollection markdown
    @OutputFile File html
    
    @Input String encoding
    
    @TaskAction
    void markdown() {
        PegDownProcessor processor = new PegDownProcessor()
        File markdownFile = markdown.singleFile
        String markdownText = markdownFile.getText(encoding)
        String htmlText = processor.markdownToHtml(markdownText)

        html.parentFile.mkdirs()
        html.write(htmlText, encoding)
    }
}
