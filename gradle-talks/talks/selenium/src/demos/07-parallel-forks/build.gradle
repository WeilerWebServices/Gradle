apply plugin: "groovy"
apply plugin: "idea"

repositories {
    mavenCentral()
}

dependencies {
    testCompile localGroovy()
    testCompile 'org.testng:testng:6.8.5'
    testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:2.37.0'
    testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.37.0'
}

tasks.withType(Test) {
    useTestNG()
}

test {
    onlyIf { false } // don't run these tests
}

["chrome", "firefox", "ie"].each { driver ->
    task "test${driver.capitalize()}"(type: Test) { driverTest ->
        test.dependsOn driverTest
        testClassesDir = test.testClassesDir
        classpath = test.classpath
        reports {
            html.destination = reporting.file("$name/html")
            junitXml.destination = file("$buildDir/test-results/$name")
        }
        systemProperty "driver", driver
        systemProperty "reports", "$buildDir/browser-reports/$name"
        systemProperty "seleniumServerUrl", "http://localhost:4444/wd/hub"

        maxParallelForks 3
        jvmArgs "-Xmx64m"
    }
}
