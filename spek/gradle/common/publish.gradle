apply plugin: 'com.jfrog.bintray'

String propOrEnv(String name) {
    String property = project.findProperty(name)
    if (property == null) {
        property = System.getenv(name)
    }
    return property
}

def bintrayUser = propOrEnv("BINTRAY_USER")
def bintrayApiKey = propOrEnv("BINTRAY_API_KEY")

def bintrayRepo = 'spek-dev'
def doPublish = true

if (project.ext.releaseMode) {
    bintrayRepo = 'spek'
    doPublish = false
}

def artifacts = ext.artifacts
bintray {
    user = bintrayUser
    key = bintrayApiKey
    publish = doPublish
    pkg {
        repo = bintrayRepo
        desc = "Test framework for Kotlin"
        name = 'spek2'
        userOrg = 'spekframework'
        licenses = ['BSD New']
        labels = ['kotlin', 'testing']
        vcsUrl = 'https://github.com/spekframework/spek.git'
        githubRepo = 'spekframework/spek'
        version {
            name = rootProject.version
        }
    }

    publications = artifacts
}